
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Автор) Тогда	
		Объект.Автор = ПараметрыСеанса.ТекущийПользователь;  	
	КонецЕсли;
	
	УстановитьДоступностьЭлементовФормы();
	
	Если Объект.Ссылка.Пустая() Тогда
		
		Если Параметры.Свойство("Начало") Тогда 
			Объект.ДатаЗаписи = Параметры.Начало;
		КонецЕсли;
		
		Если Параметры.Свойство("Окончание") Тогда
			Объект.ДатаОкончанияЗаписи = Параметры.Окончание;
		КонецЕсли;
		
		Если Параметры.Свойство("Сотрудник") Тогда
			Объект.Сотрудник = Параметры.Сотрудник;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Объект.ДатаЗаписи) Тогда
			Объект.Дата = НачалоДня(Объект.ДатаЗаписи);
		КонецЕсли;    
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиУслугаПриИзменении(Элемент)
	СтрокаТЧ = Элементы.Услуги.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(СтрокаТЧ.Услуга) Тогда
		СтрокаТЧ.Стоимость = РаботаСЦенамиВызовСервера.ПолучитьЦенуПродажиНаДату(СтрокаТЧ.Услуга, ,Объект.Дата);
	Иначе
		СтрокаТЧ.Стоимость = 0;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементовФормы()
	
	Если НЕ РольДоступна("ПолныеПрава") Тогда
		Элементы.УслугаОказана.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Записан заказ");
	
КонецПроцедуры





